/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id "com.google.protobuf" version "0.8.14"
}

description = 'Ballerina - Choreo Extension - Native Module'

dependencies {
    implementation "org.ballerinalang:ballerina-runtime:${ballerinaLangVersion}"
    implementation "org.ballerinalang:ballerina-config:${ballerinaLangVersion}"
    implementation "io.jaegertracing:jaeger-core:${jaegerVersion}"
    implementation("io.grpc:grpc-protobuf:${grpcVersion}") {
        exclude group: 'io.grpc', module: 'grpc-context'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    implementation("io.grpc:grpc-stub:${grpcVersion}") {
        exclude group: 'io.grpc', module: 'grpc-context'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'

    }
    implementation("io.grpc:grpc-netty-shaded:${grpcVersion}") {
        exclude group: 'io.grpc', module: 'grpc-context'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    implementation "javax.annotation:javax.annotation-api:1.3.2"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId "choreo-extension-native"
            version = project.version
            artifact jar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("packagePublishRepo")}")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }
}

def reporterPropertiesFile = new File("${project.projectDir}/src/main/resources/choreo-extension.properties")
def originalProperties = reporterPropertiesFile.text

task updateReporterProperties {
    doLast {
        def tomlVersion = project.version.split("-")[0]

        def newProperties = reporterPropertiesFile.text.replace("@toml.version@", tomlVersion)
        reporterPropertiesFile.text = newProperties
    }
}

task revertReporterProperties {
    doLast {
        reporterPropertiesFile.text = originalProperties
    }
}

processResources.finalizedBy revertReporterProperties
processResources.dependsOn updateReporterProperties
